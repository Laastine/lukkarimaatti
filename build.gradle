apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    querydslapt
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

war {
    exclude(
            'app/css',
            'app/js',
            'app/res',
            'app/img',
            'app/build',
            'app/dist/build'
    )
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

clean {
    delete sourceSets.generated.java.srcDirs
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

test {
    maxHeapSize = "2048m"
    testLogging.showStandardStreams = true
    testLogging {
        events 'passed'
    }
}

ext {
    springVersion = '4.0.7.RELEASE'
    seleniumVersion = '2.45.0'
    jettyVersion = '9.2.11.v20150529'
}

configurations.all {
    exclude module: 'slf4j-log4j12'
}

dependencies {

    compile 'org.apache.commons:commons-lang3:3.3.2',
            'commons-io:commons-io:2.4',
            'commons-validator:commons-validator:1.4.0',
            'cglib:cglib:3.1',
            'asm:asm:3.3.1',
            'org.javassist:javassist:3.18.1-GA',
            "org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.0.Final",
            "org.hibernate:hibernate-entitymanager:4.3.6.Final",
            'org.springframework.data:spring-data-rest-core:2.2.0.RELEASE',
            'org.springframework.data:spring-data-rest-webmvc:2.2.0.RELEASE',
            "org.springframework.data:spring-data-jpa:1.7.1.RELEASE",
            'com.mysema.querydsl:querydsl-jpa:3.3.2',
            'postgresql:postgresql:9.1-901-1.jdbc4',
            'org.jsoup:jsoup:1.8.1',
            'org.codehaus.jackson:jackson-mapper-asl:1.9.13',
            'javax.mail:mail:1.4.7',
            'log4j:log4j:1.2.17',
            'org.slf4j:slf4j-simple:1.7.12'

    querydslapt "com.mysema.querydsl:querydsl-apt:3.3.2"

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    runtime 'jstl:jstl:1.2'

    testCompile 'junit:junit:4.11',
            'org.hamcrest:hamcrest-all:1.3',
            "org.springframework:spring-test:$springVersion",
            'org.mockito:mockito-all:1.9.5',
            'javax.servlet:javax.servlet-api:3.1.0',

            "org.eclipse.jetty:jetty-webapp:${jettyVersion}"

    testRuntime 'com.h2database:h2:1.4.186'
}
