apply plugin: 'java'
apply plugin: 'jetty'
apply plugin: 'idea'
apply plugin: 'war'

group = 'org.ltky'

repositories {
    mavenCentral()
}

jettyRun {
    httpPort = 8085
    scanIntervalSeconds = 2
}

war {
    exclude('WEB-INF/static/tests',
            'WEB-INF/static/build',
            'WEB-INF/static/css',
            'WEB-INF/static/js',
            'WEB-INF/static/res',
            'WEB-INF/static/build',
    )
}

test {
    exclude '**/*IntegrationTest*'
    testLogging.showStandardStreams = true
    testLogging {
        events 'passed'
    }
}

task integrationTest(type: Test) {
    include '**/*IntegrationTest*'
    doFirst {
        jettyRun.contextPath = '/lukkarimaatti';
        jettyRun.httpPort = 8085    // Port for test
        jettyRun.daemon = true
        jettyRun.execute()
    }
    doLast {
        jettyStop.stopPort = 8091   // Port for stop signal
        jettyStop.stopKey = 'stopKey'
        jettyStop.execute()
    }
}

ext {
    springVersion = '4.0.3.RELEASE'
    jerseyVersion = '1.9'
}

dependencies {

    compile 'org.apache.commons:commons-lang3:3.1',
            'commons-io:commons-io:2.4',
            'cglib:cglib:3.1',
            'asm:asm:3.3.1',
            "org.springframework:spring-core:$springVersion",
            "org.springframework:spring-web:$springVersion",
            "org.springframework:spring-webmvc:$springVersion",
            "org.springframework:spring-orm:$springVersion",
            "org.springframework:spring-beans:$springVersion",
            "org.springframework:spring-jdbc:$springVersion",
            "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-context-support:$springVersion",
            'org.hibernate:hibernate-core:4.3.4.Final',
            'org.hibernate:hibernate-validator:5.1.0.Final',
            'org.javassist:javassist:3.18.1-GA',
            'log4j:log4j:1.2.17',
            'org.slf4j:slf4j-log4j12:1.7.6',
            'postgresql:postgresql:9.1-901.jdbc4',
            'org.jsoup:jsoup:1.7.3',
            "com.sun.jersey:jersey-core:$jerseyVersion",
            "com.sun.jersey:jersey-server:$jerseyVersion",
            "com.sun.jersey:jersey-json:$jerseyVersion"

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    runtime 'jstl:jstl:1.2'

    testCompile 'junit:junit:4.11',
            'org.hamcrest:hamcrest-all:1.3',
            "org.springframework:spring-test:$springVersion",
            "com.sun.jersey:jersey-client:$jerseyVersion",
            "com.sun.jersey:jersey-test-framework:$jerseyVersion"

}
